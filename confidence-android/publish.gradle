if (project.hasProperty('SONATYPE_USERNAME') && project.hasProperty('SONATYPE_PASSWORD')) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    afterEvaluate {
        publishing {
            repositories {
                maven {
                    name project.version.toString().endsWith("-SNAPSHOT") ? 'snapshot' : 'release'
                    url project.version.toString().endsWith("-SNAPSHOT") ? SNAPSHOT_REPOSITORY : RELEASE_REPOSITORY
                    credentials {
                        username SONATYPE_USERNAME
                        password SONATYPE_PASSWORD
                    }
                }
            }

            publications {
                release(MavenPublication) {
                    from components.release

                    pom {
                        name = project.name
                        description = POM_DESCRIPTION
                        url = POM_PROJECT_URL
                        scm {
                            url = POM_SCM_URL
                            connection = POM_SCM_CONNECTION
                            developerConnection = POM_SCM_DEVELOPER_CONNECTION
                        }
                        licenses {
                            license {
                                name = POM_LICENSE
                                url = POM_LICENSE_URL
                            }
                        }
                        developers {
                            developer {
                                id = POM_DEVELOPER_ID
                                name = POM_DEVELOPER_NAME
                                email = POM_DEVELOPER_EMAIL
                                organization = POM_DEVELOPER_ORGANIZATION
                                organizationUrl = POM_DEVELOPER_ORGANIZATION_URL
                            }
                        }
                    }
                }
            }
        }

        signing {
            sign publishing.publications
        }
    }
}